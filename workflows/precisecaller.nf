/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT MODULES / SUBWORKFLOWS / FUNCTIONS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
include { FASTQC                                                                } from '../modules/nf-core/fastqc/main'
include { MULTIQC                                                               } from '../modules/nf-core/multiqc/main'
include { paramsSummaryMap                                                      } from 'plugin/nf-schema'
include { paramsSummaryMultiqc                                                  } from '../subworkflows/nf-core/utils_nfcore_pipeline'
include { softwareVersionsToYAML                                                } from '../subworkflows/nf-core/utils_nfcore_pipeline'
include { methodsDescriptionText                                                } from '../subworkflows/local/utils_nfcore_precisecaller_pipeline'
include { FASTP                                                                 } from '../modules/nf-core/fastp/main'
include { BWA_MEM                                                               } from '../modules/nf-core/bwa/mem/main'
include { SAMTOOLS_SORT                                                         } from '../modules/nf-core/samtools/sort/main'
include { SAMTOOLS_VIEW                            as SAMTOOLS_FILTER_MAPQ      } from '../modules/nf-core/samtools/view/main'
include { PICARD_MARKDUPLICATES                                                 } from '../modules/nf-core/picard/markduplicates/main'
include { FASTQ_FILTER_UMI_CONSENSUS_FGBIO                                      } from '../subworkflows/local/fastq_filter_umi_consensus_fgbio/main'
include { FASTQ_SPLIT_SEQKIT                                                    } from '../subworkflows/local/fastq_split_seqkit/main'
include { BAM_MERGE_SAMTOOLS                                                    } from '../subworkflows/local/bam_merge_samtools/main'
include { BAM_COLLECT_METRICS_PICARD               as ALIGN_METRICS_POST_SORT   } from '../subworkflows/local/bam_collect_metrics_picard/main'
include { BAM_COLLECT_METRICS_PICARD               as ALIGN_METRICS_POST_MAPQ   } from '../subworkflows/local/bam_collect_metrics_picard/main'
include { BAM_COLLECT_METRICS_MOSDEPTH             as COVERAGE_METRICS          } from '../subworkflows/local/bam_collect_metrics_mosdepth/main'
include { BAM_BASERECALIBRATOR_APPLYBQSR_GATK      as BASERECALIBRATOR          } from '../subworkflows/local/bam_baserecalibrator_applybqsr_gatk/main'
include { BAM_VARIANT_CALLING_HAPLOTYPECALLER_GATK as HAPLOTYPECALLER           } from '../subworkflows/local/bam_variant_calling_haplotypecaller_gatk/main'
include { VCF_GENOTYPE_GATK                        as GENOTYPE                  } from '../subworkflows/local/vcf_genotype_gatk/main'
include { VCF_VARIANTFILTRATION_GATK               as VARIANTFILTRATION         } from '../subworkflows/local/vcf_variantfiltration_gatk/main'
include { VCF_SELECTVARIANTS_BY_SAMPLE_GATK        as SELECTVARIANTS_BY_SAMPLE  } from '../subworkflows/local/vcf_selectvariants_by_sample_gatk/main'
include { VCF_NORM_BCFTOOLS                        as VARIANTNORMALIZATION      } from '../subworkflows/local/vcf_norm_bcftools/main'
include { VCF_ANNOTATION_VEP                       as VARIANTANNOTATION         } from '../subworkflows/local/vcf_annotation_vep/main'

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    RUN MAIN WORKFLOW
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow PRECISECALLER {
    take:
    samplesheet // channel: samplesheet read in from --input
    fasta
    fasta_fai
    dict
    bwa
    dbsnp
    dbsnp_tbi
    known_indels
    known_indels_tbi
    known_snps
    known_snps_tbi
    vep_cache
    vep_cache_version
    vep_genome
    vep_species
    intervals
    intervals_gz_tbi
    umi_file
    filters_indel_map
    filters_snp_map
    versions

    main:
    multiqc_files = Channel.empty()

    /*
    ~~~~~~~~~~~~~~~~~~~~~~
       PREPROCESSING
    ~~~~~~~~~~~~~~~~~~~~~~
    */

    // Add readgroup to meta
    fastq = samplesheet.map { meta, files ->
        def rg = ReadGroup.extractFromFastq(
            meta,
            files,
            params.seq_center,
            params.seq_platform
        )

        assert rg :
            "Failed to build read group for sample ${meta.id}"

        def rg_sam = ReadGroup.toSam(rg)

        tuple(meta + [ read_group: rg, read_group_sam: "'${rg_sam}'" ], files)
    }

    // FASTQC is intentionally run before FASTQ splitting to avoid
    // duplicating QC metrics across chunks
    FASTQC(fastq)

    multiqc_files = multiqc_files.mix(FASTQC.out.zip.map { meta, zip -> zip })
    versions      = versions.mix(FASTQC.out.versions.first())

    // The BAM file will be generated by umi pipeline or by
    // alignment
    bam     = Channel.empty()
    bam_bai = Channel.empty()
    index_format = 'bai'

    // Determine which preprocessing steps should be executed based on
    // user parameters. UMI processing is mutually exclusive with trimming
    enable_umi   = params.umi_read_structure
    enable_split = params.split_fastq_reads > 0
    enable_trim  = !enable_umi && !params.skip_trimming

    // Alignment is required only when no BAM has been produced upstream
    enable_align = !enable_umi

    // Enable MAPQ-based read filtering after coordinate sorting
    // A value of 0 disables filtering entirely, preserving all alignments
    enable_mapq_filter = params.min_mapq > 0

    // Enable alignment metrics collection
    enable_collect_alignment_metrics = !params.skip_alignment_metrics

    // Enable coverage metrics collection
    enable_collect_coverage_metrics = !params.skip_coverage_metrics

    // Enable base recalibration metrics collection
    enable_collect_recalibration_metrics = !params.skip_recalibration_metrics

    // Split FASTQ files into smaller chunks for improved parallelization.
    // This step is purely structural: it does not modify read sequences,
    // qualities, or metadata other than adding `meta.chunk`.
    if (enable_split) {
        FASTQ_SPLIT_SEQKIT(fastq)

        fastq    = FASTQ_SPLIT_SEQKIT.out.reads
        versions = versions.mix(FASTQ_SPLIT_SEQKIT.out.versions)
    }

    // Perform read trimming and filtering using FastP
    // This step is skipped when UMI processing is enabled, as trimming may
    // interfere with UMI read structures
    if (enable_trim) {
        save_trimmed_fail = false
        save_merged       = false

        FASTP(
            fastq,
            [],                // no adapter
            false,             // no discard_trimmed_pass
            save_trimmed_fail,
            save_merged
        )

        fastq         = FASTP.out.reads
        versions      = versions.mix(FASTP.out.versions)
        multiqc_files = multiqc_files.mix(FASTP.out.json.map { meta, json -> json })
        multiqc_files = multiqc_files.mix(FASTP.out.html.map { meta, html -> html })
    }

    // UMI consensus calling
    if (enable_umi) {
        FASTQ_FILTER_UMI_CONSENSUS_FGBIO(
            fastq,
            fasta,
            dict,
            bwa,
            umi_file,
            params.umi_file_max_mismatches,
            params.umi_file_min_distance,
            params.group_by_umi_strategy,
            params.umi_min_reads,
            params.umi_min_base_quality,
            params.umi_max_base_error_rate
        )

        bam           = FASTQ_FILTER_UMI_CONSENSUS_FGBIO.out.bam
        versions      = versions.mix(FASTQ_FILTER_UMI_CONSENSUS_FGBIO.out.versions)
        multiqc_files = multiqc_files.mix(FASTQ_FILTER_UMI_CONSENSUS_FGBIO.out.histogram.map { meta, txt -> txt })
    }

    // Align reads to the reference genome when UMI processing is not enabled
    // If UMI consensus calling was performed, alignment is already included
    // in the UMI subworkflow and should not be repeated
    if (enable_align) {
        sort = false
        BWA_MEM(fastq, bwa, [[id:'no_fasta'], []], sort)

        bam      = BWA_MEM.out.bam
        versions = versions.mix(BWA_MEM.out.versions)
    }

    // Merge all BAM files belonging to the same biological sample
    // This step consolidates data from FASTQ chunks or parallel alignments
    // into a single BAM per sample
    BAM_MERGE_SAMTOOLS(bam)

    bam      = BAM_MERGE_SAMTOOLS.out.bam
    versions = versions.mix(BAM_MERGE_SAMTOOLS.out.versions)

    // Sort merged BAM files by genomic coordinate and generate a BAI index
    // Coordinate sorting is required for downstream analysis steps
    SAMTOOLS_SORT(
        bam,
        [[id:'no_fasta'], []],
        index_format
    )

    bam     = SAMTOOLS_SORT.out.bam
    bam_bai = SAMTOOLS_SORT.out.bai
    // versions from topic channel

    // Collect alignment and insert size metrics on coordinate-sorted BAMs
    // Metrics are labeled as `post_sort` to distinguish them from
    // post-filtering metrics when MAPQ filtering is enabled
    if (enable_collect_alignment_metrics) {
        ALIGN_METRICS_POST_SORT(
            bam.map { meta, bam ->
                [ meta + [ stage: 'post_sort' ], bam ]
            },
            fasta
        )

        versions      = versions.mix(ALIGN_METRICS_POST_SORT.out.versions)
        multiqc_files = multiqc_files.mix(ALIGN_METRICS_POST_SORT.out.alignment_metrics.map { meta, txt -> txt })
        multiqc_files = multiqc_files.mix(ALIGN_METRICS_POST_SORT.out.insert_metrics.map    { meta, txt -> txt })
    }

    // Filter alignments based on minimum MAPQ threshold
    // Since `samtools view` preserves coordinate order, the output BAM
    // remains sorted and can be indexed directly
    if (enable_mapq_filter) {
        SAMTOOLS_FILTER_MAPQ(
            bam.map { meta, bam -> [ meta, bam, [] ] },
            [[id:'no_fasta'], []],
            [], // No qname file
            index_format
        )

        bam      = SAMTOOLS_FILTER_MAPQ.out.bam
        bam_bai  = SAMTOOLS_FILTER_MAPQ.out.bai
        // versions from topic channel

        // Metrics are labeled as `post_mapq` to distinguish them from
        // post-sorting metrics
        if (enable_collect_alignment_metrics) {
            ALIGN_METRICS_POST_MAPQ(
                bam.map { meta, bam ->
                    [ meta + [ stage: 'post_mapq' ], bam ]
                },
                fasta
            )

            versions      = versions.mix(ALIGN_METRICS_POST_MAPQ.out.versions)
            multiqc_files = multiqc_files.mix(ALIGN_METRICS_POST_MAPQ.out.alignment_metrics.map { meta, txt -> txt })
            multiqc_files = multiqc_files.mix(ALIGN_METRICS_POST_MAPQ.out.insert_metrics.map    { meta, txt -> txt })
        }
    }

    // Mark duplicated reads
    PICARD_MARKDUPLICATES(bam, fasta, fasta_fai)

    bam           = PICARD_MARKDUPLICATES.out.bam
    bam_bai       = PICARD_MARKDUPLICATES.out.bai
    versions      = versions.mix(PICARD_MARKDUPLICATES.out.versions)
    multiqc_files = multiqc_files.mix(PICARD_MARKDUPLICATES.out.metrics.map { meta, txt -> txt })

    // Collect coverage metrics
    if (enable_collect_coverage_metrics) {
        COVERAGE_METRICS(bam, bam_bai, intervals)

        versions      = versions.mix(COVERAGE_METRICS.out.versions)
        multiqc_files = multiqc_files.mix(COVERAGE_METRICS.out.global_txt.map  { meta, txt -> txt })
        multiqc_files = multiqc_files.mix(COVERAGE_METRICS.out.summary_txt.map { meta, txt -> txt })
        multiqc_files = multiqc_files.mix(COVERAGE_METRICS.out.regions_txt.map { meta, txt -> txt })
    }

    // Run GATK4 Base Recalibration on dedupped BAMs
    BASERECALIBRATOR(
        bam,
        bam_bai,
        intervals,
        fasta,
        fasta_fai,
        dict,
        known_indels,
        known_indels_tbi,
        known_snps,
        known_snps_tbi,
        enable_collect_recalibration_metrics
    )

    bam      = BASERECALIBRATOR.out.bam
    bam_bai  = BASERECALIBRATOR.out.bai
    versions = versions.mix(BASERECALIBRATOR.out.versions)

    /*
    ~~~~~~~~~~~~~~~~~~~~~~
       VARIANT CALLING
    ~~~~~~~~~~~~~~~~~~~~~~
    */

    vcf     = Channel.empty()
    vcf_tbi = Channel.empty()

    // GATK HaplotypeCaller calls variants (SNPs and INDELs)
    HAPLOTYPECALLER(
        bam,
        bam_bai,
        intervals,
        fasta,
        fasta_fai,
        dict,
        dbsnp,
        dbsnp_tbi,
    )

    vcf      = HAPLOTYPECALLER.out.vcf
    vcf_tbi  = HAPLOTYPECALLER.out.tbi
    versions = versions.mix(HAPLOTYPECALLER.out.versions)

    // Joint genotyping of multiple GVCFs using GATK
    GENOTYPE(
        vcf,
        vcf_tbi,
        intervals,
        intervals_gz_tbi,
        fasta,
        fasta_fai,
        dict,
        dbsnp,
        dbsnp_tbi,
        params.use_genomicsdb
    )

    vcf      = GENOTYPE.out.vcf
    vcf_tbi  = GENOTYPE.out.tbi
    versions = versions.mix(GENOTYPE.out.versions)

    // Split multi-sample genotyped GVCF into SNPs & INDELs
    // and apply hard-filter
    VARIANTFILTRATION(
        vcf,
        vcf_tbi,
        intervals,
        fasta,
        fasta_fai,
        dict,
        filters_indel_map,
        filters_snp_map
    )

    vcf      = VARIANTFILTRATION.out.vcf
    vcf_tbi  = VARIANTFILTRATION.out.tbi
    versions = versions.mix(VARIANTFILTRATION.out.versions)

    // Separate hard-filtered, multi-sample SNP/INDEL GVCFs by sample
    SELECTVARIANTS_BY_SAMPLE(
        vcf,
        vcf_tbi,
        intervals
    )

    vcf      = SELECTVARIANTS_BY_SAMPLE.out.vcf
    vcf_tbi  = SELECTVARIANTS_BY_SAMPLE.out.tbi
    versions = versions.mix(SELECTVARIANTS_BY_SAMPLE.out.versions)

    // Split multiallelic variants on GVCFs
    VARIANTNORMALIZATION(
        vcf,
        vcf_tbi,
        fasta
    )

    vcf      = VARIANTNORMALIZATION.out.vcf
    vcf_tbi  = VARIANTNORMALIZATION.out.tbi
    versions = versions.mix(VARIANTNORMALIZATION.out.versions)

    // Annotates normalized and indexed VCFs using Ensembl VEP
    VARIANTANNOTATION(
        vcf,
        vcf_tbi,
        fasta,
        vep_cache,
        vep_cache_version,
        vep_genome,
        vep_species
    )

    vcf      = VARIANTANNOTATION.out.vcf
    vcf_tbi  = VARIANTANNOTATION.out.tbi
    // versions from topic channel
    // multiqc_files from topic channel

    //
    // IS THIS THE END?
    //

    //
    // Capture and format the topic-based versions
    //
    topic_versions = Channel.topic('versions')
        .map { proc, tool, version ->
            "\"${proc}\":\n    ${tool}: ${version}"
        }
        .collectFile(name: 'topic_versions.yml', newLine: true)
        .ifEmpty([])

    // Mix them into the main versions channel
    // This combines legacy .out.versions and the new topic versions
    versions = versions.mix(topic_versions)

    //
    // Collate and save software versions
    //
    softwareVersionsToYAML(versions)
        .collectFile(
            storeDir: "${params.outdir}/pipeline_info",
            name:  ''  + 'pipeline_software_' +  'mqc_'  + 'versions.yml',
            sort: true,
            newLine: true
        ).set { collated_versions }

    //
    // Capture and format the topic-based multiqc_files
    //
    topic_multiqc_files = Channel.topic('multiqc_files')
        .map { it[2] }
        .ifEmpty([])

    // Mix them into the main multiqc_files channel
    // This combines legacy and the new topic reports
    multiqc_files = multiqc_files.mix(topic_multiqc_files)

    //
    // MODULE: MultiQC
    //
    multiqc_config = Channel.fromPath("$projectDir/assets/multiqc_config.yml", checkIfExists: true)

    multiqc_custom_config = params.multiqc_config ?
        Channel.fromPath(params.multiqc_config, checkIfExists: true) :
        Channel.empty()

    multiqc_logo = params.multiqc_logo ?
        Channel.fromPath(params.multiqc_logo, checkIfExists: true) :
        Channel.empty()

    summary_params = paramsSummaryMap(workflow, parameters_schema: "nextflow_schema.json")
    workflow_summary = Channel.value(paramsSummaryMultiqc(summary_params))
    multiqc_files = multiqc_files.mix(workflow_summary.collectFile(name: 'workflow_summary_mqc.yaml'))

    multiqc_custom_methods_description = params.multiqc_methods_description ?
        file(params.multiqc_methods_description, checkIfExists: true) :
        file("$projectDir/assets/methods_description_template.yml", checkIfExists: true)

    methods_description = Channel.value(methodsDescriptionText(multiqc_custom_methods_description))

    multiqc_files = multiqc_files.mix(collated_versions)
    multiqc_files = multiqc_files.mix(
        methods_description.collectFile(
            name: 'methods_description_mqc.yaml',
            sort: true
        )
    )

    MULTIQC(
        multiqc_files.collect(),
        multiqc_config.toList(),
        multiqc_custom_config.toList(),
        multiqc_logo.toList(),
        [],
        []
    )

    emit:
    multiqc_report = MULTIQC.out.report.toList() // channel: /path/to/multiqc_report.html
    versions       = versions                    // channel: [ path(versions.yml) ]
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    THE END
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
